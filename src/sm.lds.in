#include "parameters.h"

OUTPUT_ARCH( "riscv" )
ENTRY( sm_entry )

MEMORY
{
  SM_STATE (w) :  ORIGIN = SM_STATE_ADDR, LENGTH = SM_STATE_LEN
  SM (wx) :       ORIGIN = SM_ADDR,       LENGTH = SM_LEN
}

SECTIONS
{
  . = ORIGIN(SM_STATE);
  .sm_state : {
    PROVIDE( sm_state = . );
    *(.sm.state)
  } > SM_STATE
}

SECTIONS
{
  . = ORIGIN(SM);
  .sm.text : {
    PROVIDE( sm_entry = . );
    KEEP( *(.text.entry) )
    *(.text.*)
    *(.text)
  } > SM

  .sm.rodata : {
    *(.rodata)  /* read-only data */
    *(.rodata.*)
  } > SM

  .sm.data : {
    *(.data)  /* initialized data */
    *(.data.*)
    *(.bss)   /* uninitialized data */
    *(.bss.*)
  } > SM
}
